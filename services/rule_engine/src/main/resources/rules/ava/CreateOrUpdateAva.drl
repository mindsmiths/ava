package rules.ava;

import com.mindsmiths.employeeManager.employees.Employee;
import com.mindsmiths.sdk.utils.Utils;
import com.mindsmiths.ruleEngine.util.Agents;

import signals.EmployeeUpdateSignal;
import signals.AllEmployees;
import signals.MatchingParametersSignal;

import agents.CultureMaster;
import agents.Ava;

import models.EmployeeProfile;
import models.OnboardingStage;

rule "Create new employee"
    when
        newEmployee: Employee() from entry-point "signals"
        agent: Ava(onboardingStage == null)
    then
        delete(newEmployee);
        modify(agent) {
            addConnection("email", newEmployee.getEmail()),
            addConnection("armory", Utils.randomGenerator()),
            setOnboardingStage(OnboardingStage.AVA_CREATED)};
        EmployeeProfile profile = new EmployeeProfile(newEmployee);
        insert(profile);
        agent.send(CultureMaster.ID, new EmployeeUpdateSignal(profile));
end

rule "Update existing employee"
    when
        employee: Employee() from entry-point "signals"
        profile: EmployeeProfile()
        agent: Ava(onboardingStage != null)
    then
        delete(employee);
        modify(agent) {addConnection("email", employee.getEmail())};
        modify(profile) {
            setFirstName(employee.getFirstName()),
            setLastName(employee.getLastName()),
            setEmail(employee.getEmail()),
            setActive(employee.getActive())};
        agent.send(CultureMaster.ID, new EmployeeUpdateSignal(profile));
end

rule "Update other employees data"
    when
        signal: AllEmployees(allEmployees: allEmployees) from entry-point "signals"
        agent: Ava()
        employee: EmployeeProfile()
    then
        allEmployees.remove(agent.getId());
        modify(agent) {
            setOtherEmployees(allEmployees),
            addConnectionStrengths()};
        agent.send(CultureMaster.ID, new MatchingParametersSignal(
                employee.getId(),
                agent.getConnectionStrengthAsValue(),
                agent.getMatchHistory()));
        delete(signal);
end