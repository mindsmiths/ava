package rules.cultureMaster;

import com.mindsmiths.ruleEngine.util.Log;
import com.mindsmiths.ruleEngine.model.Heartbeat;
import com.mindsmiths.ruleEngine.util.Agents;
import com.mindsmiths.ruleEngine.model.Initialize;
import com.mindsmiths.pairingalgorithm.Matches;
import com.mindsmiths.pairingalgorithm.LunchCompatibilities;

import signals.MatchingParametarsSignal;
import signals.EmployeeAvailabilitySignal;

import agents.CultureMaster;
import models.CmLunchCycleStage;


rule "First contact"
    salience 100
    when
        initialize: Initialize() from entry-point "agent-created"
        agent: CultureMaster()
    then
        Log.info("Created new CultureMaster agent.");
        delete(initialize);
end

rule "Store Employee availability"
    when
        signal: EmployeeAvailabilitySignal(employeeAvailability: employeeAvailability) from entry-point "signals"
        agent: CultureMaster(lunchCycleStage == CmLunchCycleStage.COLLECT_AVA_AVAILABILITIES)
    then
        modify(agent) {addEmployeeAvailability(employeeAvailability)};
        Log.info("Storing availability: " + agent.getEmployeeAvailabilities());
        delete(signal);
end

rule "Store Employee matching parametars"
    when
        signal: MatchingParametarsSignal(employeeId: employeeId, connectionStrengths: connectionStrengths,
                                         matchHistory: matchHistory) from entry-point "signals"
        agent: CultureMaster(employeeConnectionStrengths: employeeConnectionStrengths,
                             employeeMatchHistories: employeeMatchHistories)
    then
        employeeConnectionStrengths.put(employeeId, connectionStrengths);
        employeeMatchHistories.put(employeeId, matchHistory);
        update(agent)
        delete(signal);
end

rule "Generate this weeks matches"
    when
        Heartbeat(ts: timestamp) from entry-point "signals"
        agent: CultureMaster(lunchCycleStage == CmLunchCycleStage.GENERATE_MATCHES)
    then
        Log.info("Generating matches....");
        modify(agent) {setLunchCycleStage(CmLunchCycleStage.COLLECT_GENERATED_MATCHES)};
        agent.generateMatches();
end

rule "Store generated matches"
    when
        generatedMatches: Matches(allMatches: allMatches) from entry-point "signals"
        agent: CultureMaster(lunchCycleStage == CmLunchCycleStage.COLLECT_GENERATED_MATCHES)
    then
        Log.info("Matches from pairing algorithm: " + allMatches);
        modify(agent) {
            addMatches(allMatches),
            setLunchCycleStage(CmLunchCycleStage.SEND_TO_AVAS)
        };
        delete(generatedMatches);
end

rule "Send match info to Ava agents"
    when 
        agent: CultureMaster(lunchCycleStage == CmLunchCycleStage.SEND_TO_AVAS)
    then
        agent.sendMatches();
        modify(agent) {
            setLunchCycleStage(CmLunchCycleStage.FINISHED),
            clearEmployeeAvailabilities()
        };
end

rule "Restore lunch cycle"
    when
        Heartbeat(ts: timestamp) from entry-point "signals"
        agent: CultureMaster(lunchCycleStage == CmLunchCycleStage.FINISHED)
        eval(evaluateCronExpression("* * 18-19 ? * FRI *", ts, "Europe/Zagreb"))
    then
        modify(agent) {setLunchCycleStage(CmLunchCycleStage.COLLECT_AVA_AVAILABILITIES)};
end 

rule "Store lunch compatibilities"
    when
        signal: LunchCompatibilities() from entry-point "signals"
        agent: CultureMaster()
    then
        modify(agent) {setLunchCompatibilities(signal)};
end